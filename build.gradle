plugins {
    id 'ch.netzwerg.release' version '1.1.0'
    id "com.jfrog.bintray" version "1.3.1"
}

apply plugin: 'java'
apply plugin: 'maven-publish'

group = 'ch.netzwerg'

allprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {
    jcenter()
}

dependencies {
    compile 'com.google.guava:guava:18.0'
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar 
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

bintray {
    dryRun = project.hasProperty('dryRun') && project.dryRun.toBoolean()
    user = "$BINTRAY_USER"
    key = "$BINTRAY_API_KEY"
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'chabis' 
        licenses = ['unlicense']
        vcsUrl = 'https://github.com/netzwerg/chabis.git'
        version {
            name = project.version
            vcsTag = project.release.tagName
        }
    } 
}
